# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

## Build libnative_vsync.so

config("native_vsync_config") {
  visibility = [ ":*" ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]
}

config("native_vsync_public_config") {
  include_dirs =
      [ "//foundation/graphic/graphic_2d/interfaces/inner_api/composer" ]
}

ohos_shared_library("libnative_vsync") {
  sanitize = {
    boundary_sanitize = true
    integer_overflow = true
    ubsan = true
  }
  sources = [ "native_vsync.cpp" ]

  include_dirs = [
    "//foundation/graphic/graphic_2d/utils/log",
    "//foundation/graphic/graphic_2d/interfaces/inner_api/common",
  ]

  configs = [ ":native_vsync_config" ]

  public_configs = [ ":native_vsync_public_config" ]

  output_extension = "so"

  deps = [
    "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base",
    "$graphic_2d_root/rosen/modules/render_service_client:librender_service_client",
  ]

  external_deps = [
    "eventhandler:libeventhandler",
    "hilog:libhilog",
  ]

  public_external_deps = [ "c_utils:utils" ]

  part_name = "graphic_2d"
  subsystem_name = "graphic"
}

## Build native_vsync test
group("test") {
  testonly = true
  deps = [ "test:test" ]
}
