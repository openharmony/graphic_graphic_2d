# Copyright (c) 2021-2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")
import("$graphic_2d_root/rosen/modules/render_service_base/config.gni")

config("render_service_link_config") {
  if (is_ohos && is_clang && (target_cpu == "arm" || target_cpu == "arm64")) {
    ldflags = [
      "-Wl,--lto-O2",
      "-Wl,-mllvm",
      "-Wl,-wholeprogramdevirt-check=fallback",
      "-Wl,-Bsymbolic",
    ]

    if (graphic_2d_feature_enable_pgo && enable_enhanced_opt) {
      ldflags += [
        "-Wl,--aarch64-inline-plt",
        "-Wl,-mllvm,-enable-partial-inlining",
      ]
      if (graphic_2d_feature_enable_codemerge) {
        ldflags += [
          "-Wl,-plugin-opt=-split-machine-functions",
          "-Wl,-mllvm",
          "-Wl,-fixup-unconditional-branch-unsafely",
          "-Wl,--no-create-thunks-introduced-by-mfs",
          "-Wl,-mllvm",
          "-Wl,-mfs-psi-cutoff=999500",
          "-Wl,-z",
          "-Wl,keep-text-section-prefix",
          "-Wl,--symbol-ordering-file=" + rebase_path(
                  "${graphic_2d_feature_pgo_path}/librender_service_base.txt",
                  root_build_dir),
          "-Wl,--no-warn-symbol-ordering",
        ]
      }
    }
    if (target_cpu == "arm64" && defined(build_ext_path)) {
      ldflags += [ 
        "-Wl,--emit-relocs",
        "-Wl,--no-relax", 
        "-mno-fix-cortex-a53-843419" 
      ]
    }
  }
}

## Build librender_service.so
ohos_shared_library("librender_service") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
  }
  if (enhanced_opt) {
    configs = [ ":render_service_link_config" ]
  }
  defines = []
  if (graphic_2d_feature_enable_rdo) {
    defines += [ "RS_ENABLE_RDO" ]
  }
  if (graphic_2d_feature_color_gamut_enable) {
    defines += [ "PADDING_HEIGHT_32" ]
  } else {
    defines += []
  }
  if (use_musl) {
    defines += [ "FRAME_AWARE_TRACE" ]
  }
  if (use_video_processing_engine) {
    defines += [ "USE_VIDEO_PROCESSING_ENGINE" ]
  }
  defines += gpu_defines
  defines += [ "MODULE_RS" ]
  cflags = [ "-DIMAGE_COLORSPACE_FLAG" ]
  if (is_ohos && is_clang && (target_cpu == "arm" || target_cpu == "arm64")) {
    defines += [ "IS_OHOS" ]
    if (!use_libfuzzer) {
      cflags += [ "-flto=thin" ]
    }
    if (enhanced_opt) {
      if (!use_libfuzzer) {
        cflags += [ "-fwhole-program-vtables" ]
      }
      if (graphic_2d_feature_enable_pgo &&
          graphic_2d_feature_product != "default") {
        if (graphic_2d_feature_enable_codemerge) {
          defines += [ "CM_FEATURE_ENABLE" ]
        }
        cflags += [
          "-fprofile-use=" + rebase_path(
                  "${graphic_2d_feature_pgo_path}/librender_service.profdata",
                  root_build_dir),
          "-Wno-error=backend-plugin",
          "-Wno-profile-instr-out-of-date",
          "-Wno-profile-instr-unprofiled",
          "-mllvm",
          "-enable-partial-inlining",
        ]
        inputs = [ "${graphic_2d_feature_pgo_path}/librender_service.profdata" ]
      }
      if (graphic_2d_feature_product == "pc" && target_cpu == "arm64") {
        cflags += [ "-moutline-atomics" ]
      }
    }
  }

  public_deps = []

  sources = [
    "core/feature/anco_manager/rs_anco_manager.cpp",
    "core/feature/capture/rs_surface_capture_task.cpp",
    "core/feature/dirty/rs_uni_dirty_compute_util.cpp",
    "core/feature/dirty/rs_uni_dirty_occlusion_util.cpp",
    "core/feature/drm/rs_drm_util.cpp",
    "core/feature/hdr/rs_hdr_util.cpp",
    "core/feature/hwc/hpae_offline/rs_hpae_offline_layer.cpp",
    "core/feature/hwc/hpae_offline/rs_hpae_offline_processor.cpp",
    "core/feature/hwc/hpae_offline/rs_hpae_offline_process_syncer.cpp",
    "core/feature/hwc/hpae_offline/rs_hpae_offline_thread_manager.cpp",
    "core/feature/hwc/rs_uni_hwc_compute_util.cpp",
    "core/feature/hpae/rs_hpae_buffer.cpp",
    "core/feature/hpae/rs_hpae_manager.cpp",
    "core/feature/hpae/rs_hpae_render_listener.cpp",
    "core/feature/hyper_graphic_manager/hgm_context.cpp",
    "core/feature/hyper_graphic_manager/hgm_hardware_utils.cpp",
    "core/feature/hyper_graphic_manager/hgm_rp_energy.cpp",
    "core/feature/hyper_graphic_manager/rp_frame_rate_policy.cpp",
    "core/feature/hyper_graphic_manager/rp_hgm_xml_parser.cpp",
    "core/feature/hyper_graphic_manager/rs_vblank_idle_corrector.cpp",
    "core/feature/lpp/lpp_video_handler.cpp",
    "core/feature/occlusion_culling/rs_occlusion_handler.cpp",
    "core/feature/occlusion_culling/rs_occlusion_node.cpp",
    "core/feature/opinc/rs_opinc_draw_cache.cpp",
    "core/feature/render_group/rs_render_group_cache_drawable.cpp",
    "core/feature/special_layer/rs_special_layer_utils.cpp",
    "core/feature/vrate/rs_vsync_rate_reduce_manager.cpp",
    "core/feature/watermark/rs_surface_watermark.cpp",
    "core/feature/window_keyframe/rs_window_keyframe_node_drawable.cpp",
    "core/feature/chipset_vsync/rs_chipset_vsync.cpp",
    "core/pipeline/hardware_thread/rs_realtime_refresh_rate_manager.cpp",
    "core/pipeline/hwc/rs_hwc_context.cpp",
    "core/pipeline/hwc/rs_uni_hwc_visitor.cpp",
    "core/pipeline/main_thread/rs_main_thread.cpp",
    "core/pipeline/main_thread/rs_render_service_listener.cpp",
    "core/pipeline/main_thread/rs_render_service_visitor.cpp",
    "core/pipeline/render_thread/rs_base_render_engine.cpp",
    "core/pipeline/render_thread/rs_base_render_util.cpp",
    "core/pipeline/render_thread/rs_composer_adapter.cpp",
    "core/pipeline/render_thread/rs_divided_render_util.cpp",
    "core/pipeline/render_thread/rs_physical_screen_processor.cpp",
    "core/pipeline/render_thread/rs_render_engine.cpp",
    "core/pipeline/render_thread/rs_virtual_screen_processor.cpp",
    "core/pipeline/rs_pointer_window_manager.cpp",
    "core/pipeline/rs_processor.cpp",
    "core/pipeline/rs_processor_factory.cpp",
    "core/pipeline/rs_unmarshal_task_manager.cpp",
    "core/pipeline/slr_scale/rs_slr_scale.cpp",
    "core/screen_manager/rs_screen.cpp",
    "core/screen_manager/rs_screen_manager.cpp",
    "core/screen_manager/rs_screen_thread_safe_property.cpp",
    "core/system/rs_system_parameters.cpp",
    "core/transaction/rs_irender_service_connection_ipc_interface_code_access_verifier.cpp",
    "core/transaction/rs_irender_service_ipc_interface_code_access_verifier.cpp",
    "core/transaction/rs_render_service_security_utils.cpp",
    "core/transaction/rs_unmarshal_thread.cpp",
    "core/transaction/rs_client_to_render_connection.cpp",
    "core/transaction/zidl/rs_client_to_render_connection_stub.cpp",
    "main/render_server/rs_render_service.cpp",
    "main/render_server/rs_render_service_agent.cpp",
    "main/render_server/transaction/rs_client_to_service_connection.cpp",
    "main/render_server/transaction/zidl/rs_client_to_service_connection_stub.cpp",
    "main/render_server/transaction/zidl/rs_render_service_stub.cpp",
  ]

  #feature_cfg
  sources += [
    "core/feature_cfg/feature_param/extend_feature/capture_base_param.cpp",
    "core/feature_cfg/feature_param/extend_feature/color_gamut_param.cpp",
    "core/feature_cfg/feature_param/extend_feature/drm_param.cpp",
    "core/feature_cfg/feature_param/extend_feature/image_enhance_param.cpp",
    "core/feature_cfg/feature_param/extend_feature/mem_param.cpp",
    "core/feature_cfg/feature_param/extend_feature/multiscreen_param.cpp",
    "core/feature_cfg/feature_param/extend_feature/speciallayer_param.cpp",
    "core/feature_cfg/feature_param/extend_feature/surface_capture_param.cpp",
    "core/feature_cfg/feature_param/extend_feature/ui_capture_param.cpp",
    "core/feature_cfg/feature_param/extend_feature/gpu_cache_param.cpp",
    "core/feature_cfg/feature_param/feature_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/accessibility_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/dirtyregion_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/dvsync_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/filter_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/gpu_resource_release_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/hwc_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/node_mem_release_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/occlusion_culling_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/opinc_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/rotateoffscreen_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/smart_cache_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/socperf_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/spirv_cache_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/subtree_parallel_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/uifirst_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/vrate_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/video_metadata_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/vma_block_param.cpp",
    "core/feature_cfg/feature_param/performance_feature/buffer_reclaim_param.cpp",
    "core/feature_cfg/graphic_feature_param_manager.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/capture_base_param_parse.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/color_gamut_param_parse.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/drm_param_parse.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/image_enhance_param_parse.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/mem_param_parse.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/multiscreen_param_parse.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/speciallayer_param_parse.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/surface_capture_param_parse.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/ui_capture_param_parse.cpp",
    "core/feature_cfg/xml_parser/extend_feature_parser/gpu_cache_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/accessibility_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/dirtyregion_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/dvsync_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/filter_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/gpu_resource_release_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/hwc_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/node_mem_release_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/occlusion_culling_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/opinc_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/rotateoffscreen_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/smart_cache_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/socperf_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/spirv_cache_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/subtree_parallel_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/uifirst_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/vrate_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/video_metadata_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/vma_block_param_parse.cpp",
    "core/feature_cfg/xml_parser/performance_feature_parser/buffer_reclaim_param_parse.cpp",
    "core/feature_cfg/xml_parser/xml_parser_base.cpp",
  ]

  #composer
  sources += [
    "composer/composer_base/connection/rs_render_to_composer_connection.cpp",
    "composer/composer_base/layer_backend/hdi_backend.cpp",
    "composer/composer_base/layer_backend/hdi_layer.cpp",
    "composer/composer_base/layer_backend/hdi_output.cpp",
    "composer/composer_base/pipeline/rs_render_composer_agent.cpp",
    "composer/composer_base/pipeline/rs_render_composer_context.cpp",
    "composer/composer_base/pipeline/rs_render_composer_manager.cpp",
    "composer/composer_base/pipeline/rs_render_composer.cpp",
    "composer/composer_base/transaction/rs_layer_transaction_data.cpp",
    "composer/composer_base/transaction/rs_layer_transaction_handler.cpp",
    "composer/composer_client/layer/rs_surface_layer.cpp",
    "composer/composer_client/pipeline/rs_layer_context.cpp",
    "composer/composer_client/pipeline/rs_render_composer_client.cpp",
  ]

  #blurPredict
  sources += [
    "$rosen_root/modules/frame_load/src/rs_frame_blur_predict.cpp",
    "$rosen_root/modules/frame_load/src/rs_frame_deadline_predict.cpp",
  ]

   #colorspace
  sources += [
    "$rosen_root/modules/render_service_base/src/feature/hdr/rs_colorspace_util.cpp",
  ]

  #dfx
  sources += [
    "core/drawable/dfx/rs_refresh_rate_dfx.cpp",
    "core/drawable/dfx/rs_skp_capture_dfx.cpp",
  ]

  if (defined(input_ext_feature_magiccursor) && input_ext_feature_magiccursor) {
    sources += [
      "core/pipeline/magic_pointer_render/rs_magic_pointer_render_manager.cpp",
    ]
  }

  if (rs_enable_gpu) {
    sources += [
      #drawable
      "core/drawable/dfx/rs_dirty_rects_dfx.cpp",
      "core/drawable/rs_canvas_drawing_render_node_drawable.cpp",
      "core/drawable/rs_canvas_render_node_drawable.cpp",
      "core/drawable/rs_effect_render_node_drawable.cpp",
      "core/drawable/rs_logical_display_render_node_drawable.cpp",
      "core/drawable/rs_render_node_drawable.cpp",
      "core/drawable/rs_root_render_node_drawable.cpp",
      "core/drawable/rs_screen_render_node_drawable.cpp",
      "core/drawable/rs_surface_render_node_drawable.cpp",
      "core/drawable/rs_union_render_node_drawable.cpp",
      "core/feature/capture/rs_surface_capture_task_parallel.cpp",
      "core/feature/capture/rs_ui_capture_solo_task_parallel.cpp",
      "core/feature/capture/rs_ui_capture_task_parallel.cpp",
      "core/feature/capture/rs_uni_ui_capture.cpp",
      "core/feature/hwc/rs_uni_hwc_prevalidate_util.cpp",
      "core/feature/hwc_event/rs_uni_hwc_event_manager.cpp",
      "core/feature/round_corner_display/rs_rcd_render_listener.cpp",
      "core/feature/round_corner_display/rs_rcd_render_manager.cpp",
      "core/feature/round_corner_display/rs_rcd_render_visitor.cpp",
      "core/feature/round_corner_display/rs_rcd_surface_render_node.cpp",
      "core/feature/round_corner_display/rs_round_corner_config.cpp",
      "core/feature/round_corner_display/rs_round_corner_display.cpp",
      "core/feature/round_corner_display/rs_round_corner_display_manager.cpp",
      "core/feature/uifirst/rs_draw_window_cache.cpp",
      "core/feature/uifirst/rs_sub_thread_cache.cpp",
      "core/feature/uifirst/rs_uifirst_frame_rate_control.cpp",
      "core/feature/uifirst/rs_uifirst_manager.cpp",
      "core/memory/rs_canvas_dma_buffer_cache.cpp",
      "core/memory/rs_memory_manager.cpp",
      "core/pipeline/main_thread/rs_uni_render_listener.cpp",
      "core/pipeline/main_thread/rs_uni_render_visitor.cpp",
      "core/pipeline/render_thread/rs_draw_frame.cpp",
      "core/pipeline/render_thread/rs_uni_render_composer_adapter.cpp",
      "core/pipeline/render_thread/rs_uni_render_engine.cpp",
      "core/pipeline/render_thread/rs_uni_render_processor.cpp",
      "core/pipeline/render_thread/rs_uni_render_thread.cpp",
      "core/pipeline/render_thread/rs_uni_render_util.cpp",
      "core/pipeline/render_thread/rs_uni_render_virtual_processor.cpp",
    ]
  }

  if (graphic_2d_feature_enable_rdo) {
    sources += [ "core/feature/rdo/rs_rdo.cpp", ]
  }

  if (rs_enable_eglimage && rs_enable_gpu) {
    sources += [ "core/feature/gpuComposition/rs_egl_image_manager.cpp" ]
  }

  if (graphic_2d_feature_enable_vulkan) {
    sources += [ "core/pipeline/rs_vk_image_manager.cpp" ]
  }

  if ((rs_enable_eglimage && rs_enable_gpu) || graphic_2d_feature_enable_vulkan) {
    sources += [ "core/feature/gpuComposition/rs_image_manager.cpp" ]
  }

  if (graphic_2d_feature_product != "wearable") {
    defines += [ "IS_ENABLE_DRM" ]
  }

  if (graphic_2d_feature_product == "phone") {
    defines += ["PREPARE_PHASE_QUICK_SKIP_ENABLED"]
  }

  if (rs_enable_parallel_render && rs_enable_gpu) {
    sources += [
      "core/feature/uifirst/rs_sub_thread.cpp",
      "core/feature/uifirst/rs_sub_thread_manager.cpp",
      "core/pipeline/parallel_render/rs_render_task.cpp",
    ]
  }

  if (defined(graphic_2d_feature_subtree_parallel_enable) && graphic_2d_feature_subtree_parallel_enable
      && defined(graphic_2d_subtree_configs.subtree_root)) {
    sources += graphic_2d_subtree_configs.librender_service_subtree_sources
  }

  if (use_memmgr_plugin || use_memmgr) {
    sources += [ "core/memory/rs_app_state_listener.cpp" ]
  }

  if (tp_feature_enable) {
    sources += [ "core/screen_manager/touch_screen.cpp" ]
  }

  include_dirs = [
    "core",
    "core/feature_cfg",
    "core/feature_cfg/feature_param",
    "core/feature_cfg/feature_param/extend_feature",
    "core/feature_cfg/feature_param/performance_feature",
    "core/feature_cfg/xml_parser",
    "core/feature_cfg/xml_parser/extend_feature_parser",
    "core/feature_cfg/xml_parser/performance_feature_parser",
    "core/round_corner_display",
    "core/transaction",
    "main",
    "main/render_server",
    "main/transaction",
    "$graphic_2d_root/rosen/include",
    "$graphic_2d_root/rosen/modules/2d_graphics",
    "$graphic_2d_root/rosen/modules/composer/vsync/include",
    "$graphic_2d_root/rosen/modules/composer/hdi_backend/include",
    "$graphic_2d_root/rosen/modules/frame_report/include",
    "$graphic_2d_root/rosen/modules/render_frame_trace/include",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "$graphic_2d_root/rosen/modules/utils",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/config",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/frame_rate_manager",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/hgm_screen_manager",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/soft_vsync_manager",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/core/utils",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager/frame_rate_vote",
    "$graphic_2d_root/rosen/modules/create_pixelmap_surface/include",
    "$graphic_2d_root/rosen/modules/frame_load/include",
    "$graphic_2d_root/rosen/modules/ressched/include",
    "$graphic_2d_root/utils/log",
    "$graphic_2d_root/utils/socketpair/export",
    # composer file begin
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_base/connection",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_base/layer_backend",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_base/pipeline",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_base/render_layer",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_base/transaction",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_client/layer",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_client/pipeline",
    # composer file end
    "$graphic_2d_root/rosen/modules/render_service_base/include"
  ]

  public_deps += [
    "$graphic_2d_root/rosen/modules/composer:libcomposer",
  ]

  if (graphic_2d_feature_subtree_parallel_enable && defined(graphic_2d_subtree_configs.subtree_root)) {
    include_dirs += graphic_2d_subtree_configs.librender_service_subtree_include
  }

  if (graphic_2d_feature_mhc_enable) {
    include_dirs += graphic_2d_mhc_configs.librender_service_mhc_include
  }

  deps = [
    "$graphic_2d_root/frameworks/opengl_wrapper:EGL",
    "$graphic_2d_root/frameworks/opengl_wrapper:GLESv3",
    "$graphic_2d_root/rosen/modules/composer/vsync:libvsync",
    "$graphic_2d_root/rosen/modules/create_pixelmap_surface:create_pixelmap_surface",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager:libhyper_graphic_manager",
    "$graphic_2d_root/rosen/modules/render_frame_trace:render_frame_trace",
    "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base",
    "$graphic_2d_root/utils/color_manager:color_manager",
    "$graphic_2d_root/utils:socketpair",
  ]

  if (graphic_2d_feature_enable_chipset_vsync) {
    defines += [ "RS_ENABLE_CHIPSET_VSYNC" ]
    sources += [ "core/feature/chipset_vsync/rs_chipset_vsync.cpp" ]
  }

  if (defined(graphic_2d_feature_hetero_hdr_enable) && graphic_2d_feature_hetero_hdr_enable
      && defined(graphic_2d_hetero_hdr_configs)) {
    include_dirs += graphic_2d_hetero_hdr_configs.librender_service_hetero_hdr_include
    sources += graphic_2d_hetero_hdr_configs.librender_service_hetero_hdr_sources
  }

  if (graphic_2d_feature_overlay_display_enable) {
    sources += [ "core/feature/overlay_display/rs_overlay_display_manager.cpp" ]
  }

  if (graphic_2d_feature_tv_metadata_enable) {
    sources += [ "core/feature/tv_metadata/rs_tv_metadata_manager.cpp" ]
  }

  if (rosen_is_ohos) {
    sources += [ "$rosen_root/modules/frame_report/src/rs_frame_report.cpp" ]
  } else if (!is_arkui_x) {
    sources +=
        [ "$rosen_root/modules/frame_report/src/mingw/rs_frame_report.cpp" ]
  }

  #profiler
  if (rosen_is_ohos && graphic_2d_feature_rs_enable_profiler &&
      player_framework_enable) {
    defines += [ "RS_PROFILER_ENABLED" ]
    defines += [ "RS_PROFILER_BETA_ENABLED" ]
    sources += [
      "$rosen_root/modules/render_service_profiler/rs_profiler.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_beta_recorder.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_capture_recorder.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_capturedata.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_command.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_file.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_hrp_service.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_network.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_packet.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_socket.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_telemetry.cpp",
      "$rosen_root/modules/render_service_profiler/rs_profiler_test_tree.cpp",
    ]
  }

  include_dirs += [ "$graphic_2d_root/rosen/modules/render_service_profiler" ]

  public_external_deps = [ "skia:skia_canvaskit" ]

  if (defined(rs_enable_parallel_upload) && rs_enable_parallel_upload) {
    sources += [
      "core/pipeline/parallel_render/resource/rs_upload_resource_thread.cpp",
    ]
    include_dirs += [ "$graphic_2d_root/rosen/modules/render_service/core/pipeline/parallel_render/resource" ]
    if (defined(use_rosen_drawing) && use_rosen_drawing) {
      sources += [
        "core/pipeline/parallel_render/resource/rs_resource_helper_drawing.cpp",
      ]
    } else {
      sources += [
        "core/pipeline/parallel_render/resource/rs_resource_helper_skia.cpp",
      ]
    }
  }

  if (rs_enable_gpu) {
    public_deps += [ "$graphic_2d_root:libvulkan" ]
    deps += [
      "$graphic_2d_root/frameworks/opengl_wrapper:EGL",
      "$graphic_2d_root/frameworks/opengl_wrapper:GLESv3",
    ]
  }

  if (defined(use_rosen_drawing) && use_rosen_drawing) {
    defines += [ "USE_ROSEN_DRAWING" ]
    if (rs_enable_gpu) {
      defines += [ "RS_ENABLE_GPU" ]
    }
    deps += [ "$rosen_root/modules/2d_graphics:2d_graphics" ]
  }

  public_external_deps += [
    "drivers_interface_display:libdisplay_commontype_proxy_1.0",
    "libxml2:libxml2",
  ]

  external_deps = [
    "ability_runtime:app_manager",
    "cJSON:cjson",
    "c_utils:utils",
    "drivers_interface_display:libdisplay_commontype_proxy_2.3",
    "eventhandler:libeventhandler",
    "ffrt:libffrt",
    "graphic_surface:surface",
    "graphics_effect:graphics_effect_core",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:hitrace_meter",
    "init:libbegetutil",
    "ipc:ipc_core",
    "libpng:libpng",
    "qos_manager:qos",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
    "window_manager:libwsutils",
    "qos_manager:concurrent_task_client",
  ]
  if (graphic_2d_feature_rs_enable_uni_render) {
    external_deps += [ "ability_runtime:ability_manager" ]
  }
  if (use_video_processing_engine) {
    external_deps += [ "video_processing_engine:videoprocessingengine" ]
  }
  if (defined(graphic_2d_feature_hetero_hdr_enable) && graphic_2d_feature_hetero_hdr_enable
      && defined(graphic_2d_hetero_hdr_configs)) {
    external_deps += graphic_2d_hetero_hdr_configs.librender_service_hetero_hdr_external_deps
  }

  if (defined(ddgr_ext_configs)) {
    libs = ddgr_ext_configs.ddgr_libs
    defines += ddgr_ext_configs.ddgr_defines
    sources += ddgr_ext_configs.libddgr_ext_adaptor_sources
    include_dirs += ddgr_ext_configs.libddgr_ext_adaptor_include_dirs
  }

  if (rosen_is_ohos) {
    external_deps += [
      "image_framework:image_native",
      "lz4:liblz4_shared",
    ]
  }

  if (defined(global_parts_info) &&
      defined(global_parts_info.resourceschedule_soc_perf)) {
    external_deps += [ "soc_perf:socperf_client" ]
    defines += [ "SOC_PERF_ENABLE" ]
  }

  if (defined(global_parts_info) &&
      defined(global_parts_info.resourceschedule_resource_schedule_service)) {
    external_deps += [ "resource_schedule_service:ressched_client" ]
    defines += [ "RES_SCHED_ENABLE" ]
  }

  if (defined(global_parts_info) && defined(global_parts_info.sensors_sensor)) {
    external_deps += [ "sensor:sensor_interface_native" ]
    defines += [ "RS_SUBSCRIBE_SENSOR_ENABLE" ]
  }

  if (accessibility_enable) {
    external_deps += [ "accessibility:accessibilityconfig" ]
    external_deps += [ "accessibility:accessibility_common" ]
    defines += accessibility_defines
  }

  if (use_memmgr_plugin) {
    external_deps += [ "memmgr:memmgrclient" ]
  } else if (use_memmgr) {
    external_deps += [ "memmgr:memmgrclient" ]
  }

  if (rosen_is_ohos && defined(enable_ipc_security) && enable_ipc_security) {
    defines += [ "ENABLE_IPC_SECURITY" ]
    external_deps += [ "access_token:libaccesstoken_sdk" ]
  }

  if (rosen_is_ohos && defined(enable_ipc_security_access_counter) &&
      enable_ipc_security_access_counter) {
    defines += [ "ENABLE_IPC_SECURITY_ACCESS_COUNTER" ]
  }

  if (tp_feature_enable) {
    defines += tp_defines
  }

  part_name = "graphic_2d"
  subsystem_name = "graphic"
}

## Build render_service.bin
ohos_executable("render_service") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
    if (rosen_is_ohos) {
      boundary_sanitize = true
      integer_overflow = true
      ubsan = true
    }
  }
  sources = [ "main/render_server/main.cpp" ]

  include_dirs = [
    "core",
    "$graphic_2d_root/interfaces/inner_api/common",
    "$graphic_2d_root/utils/color_manager/export",
    "$graphic_2d_root/utils/socketpair/export",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_base/connection",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_base/transaction",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_base/render_layer",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_base/layer_backend",
    "$graphic_2d_root/rosen/modules/render_service/composer/composer_client/pipeline",
  ]

  deps = [
    ":librender_service",
    "$graphic_2d_root/rosen/modules/composer:libcomposer",
    "$graphic_2d_root/rosen/modules/composer/vsync:libvsync",
    "$graphic_2d_root/rosen/modules/hyper_graphic_manager:libhyper_graphic_manager",
    "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base",
    "$graphic_2d_root/utils:socketpair",
  ]

  external_deps = [
    "c_utils:utils",
    "drivers_interface_display:libdisplay_commontype_proxy_1.0",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "ipc:ipc_core",
    "samgr:samgr_proxy",
  ]

  if (defined(global_parts_info) && defined(global_parts_info.sensors_sensor)) {
    external_deps += [ "sensor:sensor_interface_native" ]
  }

  part_name = "graphic_2d"
  subsystem_name = "graphic"
}

group("test") {
  testonly = true

  deps = [
    "composer/composer_base/layer_backend/test:test",
    "composer/composer_base/pipeline/test:test",
  ]
}
