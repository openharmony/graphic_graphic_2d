# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")
import("//foundation/graphic/graphic_2d/rosen/modules/platform/config.gni")
import(
    "//foundation/graphic/graphic_2d/rosen/modules/render_service_base/config.gni")

enable_compiler_rust = !ohos_indep_compiler_enable &&
                       ((!is_mingw && !is_mac && !is_cross_platform &&
                        !(host_os == "linux" && host_cpu == "arm64")))

config("export_config") {
  include_dirs = [
    "include",
    "${graphic_2d_root}/utils/log",
  ]
  cflags_cc = [ "-std=c++17" ]

  defines = rs_common_define
  defines += gpu_defines
  if (rs_enable_gpu) {
    defines += [ "RS_ENABLE_GPU" ]
  }
}

if (enable_compiler_rust) {
  defines = [ "ENABLE_RUST" ]
  rust_cxx("particle_cxx_gen") {
    sources = [ "rust/src/lib.rs" ]
  }

  ohos_rust_static_ffi("particle_cxx_rust") {
    sources = [ "rust/src/lib.rs" ]
    external_deps = [ "rust_cxx:lib" ]
    part_name = "graphic_2d"
    subsystem_name = "graphic"
  }

  ohos_static_library("particle_cxx_cppdeps") {
    branch_protector_ret = "pac_ret"
    sanitize = {
      cfi = false
      cfi_cross_dso = false
      debug = false
      blocklist = "./rs_base_blocklist.txt"
    }
    part_name = "graphic_2d"
    subsystem_name = "graphic"
    defines += [ "RUST_CXX_NO_EXCEPTIONS" ]
  }
}

ohos_source_set("render_service_base_src") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = false
    cfi_cross_dso = false
    debug = false
    blocklist = "./rs_base_blocklist.txt"
    if (rosen_is_ohos) {
      boundary_sanitize = true
      integer_overflow = false
      ubsan = true
    }
  }
  defines = []
  defines += gpu_defines
  defines += [ "MODULE_RSB" ]

  sources = [
    #animation
    "src/animation/rs_animation_fraction.cpp",
    "src/animation/rs_animation_manager.cpp",
    "src/animation/rs_animation_marshalling.cpp",
    "src/animation/rs_animation_rate_decider.cpp",
    "src/animation/rs_animation_timing_protocol.cpp",
    "src/animation/rs_animation_trace_utils.cpp",
    "src/animation/rs_cubic_bezier_interpolator.cpp",
    "src/animation/rs_interpolator.cpp",
    "src/animation/rs_particle_noise_field.cpp",
    "src/animation/rs_particle_ripple_field.cpp",
    "src/animation/rs_particle_velocity_field.cpp",
    "src/animation/rs_render_animation.cpp",
    "src/animation/rs_render_curve_animation.cpp",
    "src/animation/rs_render_interactive_implict_animator.cpp",
    "src/animation/rs_render_interactive_implict_animator_map.cpp",
    "src/animation/rs_render_interpolating_spring_animation.cpp",
    "src/animation/rs_render_keyframe_animation.cpp",
    "src/animation/rs_render_particle.cpp",
    "src/animation/rs_render_particle_animation.cpp",
    "src/animation/rs_render_particle_effector.cpp",
    "src/animation/rs_render_particle_emitter.cpp",
    "src/animation/rs_render_particle_system.cpp",
    "src/animation/rs_render_path_animation.cpp",
    "src/animation/rs_render_property_animation.cpp",
    "src/animation/rs_render_spring_animation.cpp",
    "src/animation/rs_render_transition.cpp",
    "src/animation/rs_render_transition_effect.cpp",
    "src/animation/rs_spring_interpolator.cpp",
    "src/animation/rs_spring_model.cpp",
    "src/animation/rs_steps_interpolator.cpp",
    "src/animation/rs_value_estimator.cpp",

    # message(depracated)
    "src/command/rs_message_processor.cpp",

    # modifier
    "src/modifier/rs_render_property.cpp",

    # modifier_ng
    "src/modifier_ng/appearance/rs_alpha_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_background_filter_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_behind_window_filter_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_blend_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_border_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_color_picker_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_compositing_filter_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_dynamic_light_up_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_foreground_filter_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_hdr_brightness_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_mask_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_material_filter_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_outline_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_particle_effect_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_pixel_stretch_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_point_light_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_shadow_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_use_effect_render_modifier.cpp",
    "src/modifier_ng/appearance/rs_visibility_render_modifier.cpp",
    "src/modifier_ng/background/rs_background_color_render_modifier.cpp",
    "src/modifier_ng/background/rs_background_image_render_modifier.cpp",
    "src/modifier_ng/background/rs_background_ng_shader_render_modifier.cpp",
    "src/modifier_ng/background/rs_background_shader_render_modifier.cpp",
    "src/modifier_ng/foreground/rs_env_foreground_color_render_modifier.cpp",
    "src/modifier_ng/foreground/rs_foreground_color_render_modifier.cpp",
    "src/modifier_ng/foreground/rs_foreground_shader_render_modifier.cpp",
    "src/modifier_ng/geometry/rs_bounds_clip_render_modifier.cpp",
    "src/modifier_ng/geometry/rs_bounds_render_modifier.cpp",
    "src/modifier_ng/geometry/rs_frame_clip_render_modifier.cpp",
    "src/modifier_ng/geometry/rs_frame_render_modifier.cpp",
    "src/modifier_ng/geometry/rs_transform_render_modifier.cpp",
    "src/modifier_ng/rs_render_modifier_ng.cpp",

    #command
    "src/command/rs_animation_command.cpp",
    "src/command/rs_base_node_command.cpp",
    "src/command/rs_canvas_drawing_node_command.cpp",
    "src/command/rs_canvas_node_command.cpp",
    "src/command/rs_command_factory.cpp",
    "src/command/rs_command_verify_helper.cpp",
    "src/command/rs_display_node_command.cpp",
    "src/command/rs_effect_node_command.cpp",
    "src/command/rs_frame_rate_linker_command.cpp",
    "src/command/rs_node_command.cpp",
    "src/command/rs_node_showing_command.cpp",
    "src/command/rs_proxy_node_command.cpp",
    "src/command/rs_root_node_command.cpp",
    "src/command/rs_surface_node_command.cpp",
    "src/command/rs_union_node_command.cpp",

    #offscreen_render
    "src/offscreen_render/rs_offscreen_render_thread.cpp",

    #common
    "src/common/rs_background_thread.cpp",
    "src/common/rs_color.cpp",
    "src/common/rs_common_def.cpp",
    "src/common/rs_common_tools.cpp",
    "src/common/rs_exception_check.cpp",
    "src/common/rs_obj_abs_geometry.cpp",
    "src/common/rs_occlusion_region.cpp",
    "src/common/rs_occlusion_region_helper.cpp",
    "src/common/rs_rectangles_merger.cpp",
    "src/common/rs_special_layer_manager.cpp",
    "src/common/rs_switching_thread.cpp",
    "src/common/rs_thread_handler_generic.cpp",
    "src/common/rs_thread_looper_generic.cpp",
    "src/common/rs_thread_looper_impl.cpp",
    "src/common/rs_xcollie.cpp",

    #delegate
    "src/delegate/rs_functional_delegate.cpp",

    #ipc_callbacks
    "src/ipc_callbacks/buffer_available_callback_proxy.cpp",
    "src/ipc_callbacks/buffer_available_callback_stub.cpp",
    "src/ipc_callbacks/buffer_clear_callback_proxy.cpp",
    "src/ipc_callbacks/buffer_clear_callback_stub.cpp",
    "src/ipc_callbacks/hgm_config_change_callback_proxy.cpp",
    "src/ipc_callbacks/hgm_config_change_callback_stub.cpp",
    "src/ipc_callbacks/rs_application_agent_proxy.cpp",
    "src/ipc_callbacks/rs_application_agent_stub.cpp",
    "src/ipc_callbacks/rs_canvas_surface_buffer_callback_proxy.cpp",
    "src/ipc_callbacks/rs_canvas_surface_buffer_callback_stub.cpp",
    "src/ipc_callbacks/rs_first_frame_commit_callback_proxy.cpp",
    "src/ipc_callbacks/rs_first_frame_commit_callback_stub.cpp",
    "src/ipc_callbacks/rs_frame_rate_linker_expected_fps_update_callback_proxy.cpp",
    "src/ipc_callbacks/rs_frame_rate_linker_expected_fps_update_callback_stub.cpp",
    "src/ipc_callbacks/rs_occlusion_change_callback_proxy.cpp",
    "src/ipc_callbacks/rs_occlusion_change_callback_stub.cpp",
    "src/ipc_callbacks/rs_rt_refresh_callback.cpp",
    "src/ipc_callbacks/rs_self_drawing_node_rect_change_callback_proxy.cpp",
    "src/ipc_callbacks/rs_self_drawing_node_rect_change_callback_stub.cpp",
    "src/ipc_callbacks/rs_surface_buffer_callback.cpp",
    "src/ipc_callbacks/rs_surface_buffer_callback_proxy.cpp",
    "src/ipc_callbacks/rs_surface_buffer_callback_stub.cpp",
    "src/ipc_callbacks/rs_surface_occlusion_change_callback_proxy.cpp",
    "src/ipc_callbacks/rs_surface_occlusion_change_callback_stub.cpp",
    "src/ipc_callbacks/rs_transaction_data_callback_proxy.cpp",
    "src/ipc_callbacks/rs_transaction_data_callback_stub.cpp",
    "src/ipc_callbacks/rs_uiextension_callback_proxy.cpp",
    "src/ipc_callbacks/rs_uiextension_callback_stub.cpp",
    "src/ipc_callbacks/screen_change_callback_proxy.cpp",
    "src/ipc_callbacks/screen_change_callback_stub.cpp",
    "src/ipc_callbacks/brightness_info_change_callback_proxy.cpp",
    "src/ipc_callbacks/brightness_info_change_callback_stub.cpp",
    "src/ipc_callbacks/screen_switching_notify_callback_proxy.cpp",
    "src/ipc_callbacks/screen_switching_notify_callback_stub.cpp",
    "src/ipc_callbacks/surface_capture_callback_proxy.cpp",
    "src/ipc_callbacks/surface_capture_callback_stub.cpp",

    #ipc_security
    "src/ipc_security/rs_ipc_interface_code_access_verifier_base.cpp",

    #memory
    "src/memory/rs_memory_flow_control.cpp",
    "src/memory/rs_memory_graphic.cpp",
    "src/memory/rs_memory_snapshot.cpp",
    "src/memory/rs_memory_track.cpp",
    "src/memory/rs_tag_tracker.cpp",

    #pipeline
    "src/pipeline/rs_canvas_drawing_render_node.cpp",
    "src/pipeline/rs_canvas_render_node.cpp",
    "src/pipeline/rs_context.cpp",
    "src/pipeline/rs_dirty_region_manager.cpp",
    "src/pipeline/rs_draw_cmd.cpp",
    "src/pipeline/rs_draw_cmd_list.cpp",
    "src/pipeline/rs_effect_render_node.cpp",
    "src/pipeline/rs_logical_display_render_node.cpp",
    "src/pipeline/rs_occlusion_config.cpp",
    "src/pipeline/rs_paint_filter_canvas.cpp",
    "src/pipeline/rs_proxy_render_node.cpp",
    "src/pipeline/rs_record_cmd_utils.cpp",
    "src/pipeline/rs_recording_canvas.cpp",
    "src/pipeline/rs_render_display_sync.cpp",
    "src/pipeline/rs_render_node.cpp",
    "src/pipeline/rs_render_node_allocator.cpp",
    "src/pipeline/rs_render_node_gc.cpp",
    "src/pipeline/rs_render_node_map.cpp",
    "src/pipeline/rs_root_render_node.cpp",
    "src/pipeline/rs_screen_render_node.cpp",
    "src/pipeline/rs_surface_buffer_callback_manager.cpp",
    "src/pipeline/rs_surface_handler.cpp",
    "src/pipeline/rs_surface_render_node_utils.cpp",
    "src/pipeline/rs_surface_render_node.cpp",
    "src/pipeline/rs_task_dispatcher.cpp",
    "src/pipeline/rs_uni_render_judgement.cpp",
    "src/pipeline/rs_union_render_node.cpp",
    "src/pipeline/sk_resource_manager.cpp",

    #property
    "src/property/rs_point_light_manager.cpp",
    "src/property/rs_properties.cpp",
    "src/property/rs_properties_painter.cpp",
    "src/property/rs_property_trace.cpp",

    #effect
    "src/effect/rs_render_effect_template.cpp",
    "src/effect/rs_render_filter_base.cpp",
    "src/effect/rs_render_mask_base.cpp",
    "src/effect/rs_render_shape_base.cpp",
    "src/effect/rs_render_shader_base.cpp",

    #hpae
    "src/hpae_base/rs_hpae_base_data.cpp",
    "src/hpae_base/rs_hpae_hianimation.cpp",
    "src/hpae_base/rs_hpae_ffrt_pattern_manager.cpp",
    "src/hpae_base/rs_hpae_filter_cache_manager.cpp",
    "src/hpae_base/rs_hpae_scheduler.cpp",
    "src/hpae_base/rs_hpae_fusion_operator.cpp",

    #render
    "src/render/rs_attraction_effect_filter.cpp",
    "src/render/rs_blur_filter.cpp",
    "src/render/rs_border.cpp",
    "src/render/rs_color_adaptive_filter.cpp",
    "src/render/rs_color_extract.cpp",
    "src/render/rs_color_picker.cpp",
    "src/render/rs_colorful_shadow_filter.cpp",
    "src/render/rs_complex_shader.cpp",
    "src/render/rs_distortion_shader_filter.cpp",
    "src/render/rs_drawing_filter.cpp",
    "src/render/rs_effect_luminance_manager.cpp",
    "src/render/rs_filter.cpp",
    "src/render/rs_filter_cache_manager.cpp",
    "src/render/rs_filter_cache_memory_controller.cpp",
    "src/render/rs_flow_light_sweep_shader.cpp",
    "src/render/rs_fly_out_shader_filter.cpp",
    "src/render/rs_foreground_effect_filter.cpp",
    "src/render/rs_hdr_ui_brightness_filter.cpp",
    "src/render/rs_high_performance_visual_engine.cpp",
    "src/render/rs_hps_blur.cpp",
    "src/render/rs_image.cpp",
    "src/render/rs_image_base.cpp",
    "src/render/rs_image_cache.cpp",
    "src/render/rs_kawase_blur.cpp",
    "src/render/rs_light_up_effect_filter.cpp",
    "src/render/rs_mask.cpp",
    "src/render/rs_material_filter.cpp",
    "src/render/rs_motion_blur_filter.cpp",
    "src/render/rs_particles_drawable.cpp",
    "src/render/rs_path.cpp",
    "src/render/rs_pixel_map_shader.cpp",
    "src/render/rs_pixel_map_shader_obj.cpp",
    "src/render/rs_pixel_map_util.cpp",
    "src/render/rs_render_aibar_filter.cpp",
    "src/render/rs_render_always_snapshot_filter.cpp",
    "src/render/rs_render_filter_base.cpp",
    "src/render/rs_render_grey_filter.cpp",
    "src/render/rs_render_kawase_blur_filter.cpp",
    "src/render/rs_render_light_blur_filter.cpp",
    "src/render/rs_render_linear_gradient_blur_filter.cpp",
    "src/render/rs_render_magnifier_filter.cpp",
    "src/render/rs_render_maskcolor_filter.cpp",
    "src/render/rs_render_mesa_blur_filter.cpp",
    "src/render/rs_render_water_ripple_filter.cpp",
    "src/render/rs_shader.cpp",
    "src/render/rs_shadow.cpp",
    "src/render/rs_skia_filter.cpp",
    "src/render/rs_spherize_effect_filter.cpp",
    "src/render/rs_typeface_cache.cpp",

    #transaction
    "src/transaction/rp_hgm_config_data.cpp",
    "src/transaction/rs_hgm_config_data.cpp",
    "src/transaction/rs_occlusion_data.cpp",
    "src/transaction/rs_self_drawing_node_rect_data.cpp",
    "src/transaction/rs_transaction_data.cpp",
    "src/transaction/rs_transaction_data_callback_manager.cpp",
    "src/transaction/rs_transaction_handler.cpp",
    "src/transaction/rs_transaction_proxy.cpp",
    "src/transaction/rs_uiextension_data.cpp",

    #gfx
    "src/gfx/dump/rs_dump_manager.cpp",
    "src/gfx/first_frame_notifier/rs_first_frame_notifier.cpp",
    "src/gfx/fps_info/rs_surface_fps.cpp",
    "src/gfx/fps_info/rs_surface_fps_manager.cpp",
    "src/gfx/performance/rs_perfmonitor_reporter.cpp",

    #screen_manager
    "src/screen_manager/rs_screen_capability.cpp",
    "src/screen_manager/rs_screen_data.cpp",
    "src/screen_manager/rs_screen_hdr_capability.cpp",
    "src/screen_manager/rs_screen_mode_info.cpp",
    "src/screen_manager/rs_screen_property.cpp",
    "src/screen_manager/rs_screen_props.cpp",  
    "src/screen_manager/rs_virtual_screen_resolution.cpp",

    #info_collection
    "src/info_collection/rs_gpu_dirty_region_collection.cpp",
    "src/info_collection/rs_hardware_compose_disabled_reason_collection.cpp",
    "src/info_collection/rs_hdr_collection.cpp",
    "src/info_collection/rs_layer_compose_collection.cpp",

    #monitor
    "src/monitor/aps_monitor_impl.cpp",
    "src/monitor/self_drawing_node_monitor.cpp",

    #feature
    "src/feature/capture/rs_ui_capture_helper.cpp",
    "src/feature/color_picker/color_pick_alt_manager.cpp",
    "src/feature/color_picker/rs_color_picker_manager.cpp",
    "src/feature/color_picker/rs_color_picker_thread.cpp",
    "src/feature/color_picker/rs_color_picker_utils.cpp",
    "src/feature/color_picker/rs_hetero_color_picker.cpp",
    "src/feature/hdr/rs_colorspace_util.cpp",
    "src/feature/hyper_graphic_manager/rs_render_frame_rate_linker.cpp",
    "src/feature/hyper_graphic_manager/rs_render_frame_rate_linker_map.cpp",
    "src/feature/image_detail_enhancer/rs_image_detail_enhancer_thread.cpp",
    "src/feature/opinc/rs_opinc_cache.cpp",
    "src/feature/opinc/rs_opinc_manager.cpp",
    "src/feature/render_group/rs_render_group_cache.cpp",
    "src/feature/single_frame_composer/rs_single_frame_composer.cpp",
    "src/feature/capture/rs_capture_pixelmap_manager.cpp",
    "src/feature/window_keyframe/rs_window_keyframe_node_command.cpp",
    "src/feature/window_keyframe/rs_window_keyframe_render_node.cpp",
    "src/feature/buffer_reclaim/rs_buffer_reclaim.cpp",

    #dirty_region
    "src/dirty_region/rs_filter_dirty_collector.cpp",
    "src/dirty_region/rs_gpu_dirty_collector.cpp",
    "src/dirty_region/rs_optimize_canvas_dirty_collector.cpp",
  ]

  if (rs_enable_gpu) {
    sources += [
      #drawable
      "src/drawable/rs_color_picker_drawable.cpp",
      "src/drawable/rs_drawable.cpp",
      "src/drawable/rs_misc_drawable.cpp",
      "src/drawable/rs_property_drawable.cpp",
      "src/drawable/rs_property_drawable_background.cpp",
      "src/drawable/rs_property_drawable_foreground.cpp",
      "src/drawable/rs_point_light_drawable.cpp",
      "src/drawable/rs_property_drawable_utils.cpp",
      "src/drawable/rs_render_node_drawable_adapter.cpp",
      "src/drawable/rs_render_node_shadow_drawable.cpp",

      #params
      "src/params/rs_canvas_drawing_render_params.cpp",
      "src/params/rs_effect_render_params.cpp",
      "src/params/rs_logical_display_render_params.cpp",
      "src/params/rs_render_params.cpp",
      "src/params/rs_render_thread_params.cpp",
      "src/params/rs_screen_render_params.cpp",
      "src/params/rs_surface_render_params.cpp",
    ]
  }

  if (defined(input_ext_feature_magiccursor) && input_ext_feature_magiccursor) {
    sources += [
      #ipc_callbacks
      "src/ipc_callbacks/pointer_render/pointer_luminance_callback_ipc_interface_code_access_verifier.cpp",
      "src/ipc_callbacks/pointer_render/pointer_luminance_callback_proxy.cpp",
      "src/ipc_callbacks/pointer_render/pointer_luminance_callback_stub.cpp",
    ]
  }

  if (rosen_is_ohos) {
    sources += [
      #color_temperature
      "src/display_engine/rs_color_temperature.cpp",

      #luminance
      "src/display_engine/rs_luminance_control.cpp",
    ]
  } else {
    sources += [
      #color_temperature
      "src/display_engine/mingw/rs_color_temperature.cpp",

      #luminance
      "src/display_engine/mingw/rs_luminance_control.cpp",
    ]
  }

  if (is_cross_platform) {
    sources -= [
      #command
      "src/command/rs_display_node_command.cpp",

      #common
      "src/common/rs_thread_handler_generic.cpp",
      "src/common/rs_thread_looper_generic.cpp",
      "src/common/rs_thread_looper_impl.cpp",

      #ipc_callbacks
      "src/ipc_callbacks/buffer_available_callback_proxy.cpp",
      "src/ipc_callbacks/buffer_available_callback_stub.cpp",
      "src/ipc_callbacks/buffer_clear_callback_proxy.cpp",
      "src/ipc_callbacks/buffer_clear_callback_stub.cpp",
      "src/ipc_callbacks/hgm_config_change_callback_proxy.cpp",
      "src/ipc_callbacks/hgm_config_change_callback_stub.cpp",
      "src/ipc_callbacks/rs_occlusion_change_callback_proxy.cpp",
      "src/ipc_callbacks/rs_occlusion_change_callback_stub.cpp",
      "src/ipc_callbacks/rs_surface_occlusion_change_callback_proxy.cpp",
      "src/ipc_callbacks/rs_surface_occlusion_change_callback_stub.cpp",
      "src/ipc_callbacks/screen_change_callback_proxy.cpp",
      "src/ipc_callbacks/screen_change_callback_stub.cpp",
      "src/ipc_callbacks/surface_capture_callback_proxy.cpp",
      "src/ipc_callbacks/surface_capture_callback_stub.cpp",

      #ipc_security
      "src/ipc_security/rs_ipc_interface_code_access_verifier_base.cpp",

      #pipeline
      "src/pipeline/rs_occlusion_config.cpp",
      "src/pipeline/rs_screen_render_node.cpp",

      #transaction
      "src/transaction/rp_hgm_config_data.cpp",
      "src/transaction/rs_hgm_config_data.cpp",
      "src/transaction/rs_occlusion_data.cpp",

      #screen_manager
      "src/screen_manager/rs_screen_capability.cpp",
      "src/screen_manager/rs_screen_data.cpp",
      "src/screen_manager/rs_screen_hdr_capability.cpp",
      "src/screen_manager/rs_screen_mode_info.cpp",
      "src/screen_manager/rs_screen_props.cpp",
      "src/screen_manager/rs_virtual_screen_resolution.cpp",
    ]
  }

  cflags = [
    "-Wall",
    "-Wno-pointer-arith",
    "-Wno-non-virtual-dtor",
    "-Wno-missing-field-initializers",
    "-Wno-c++11-narrowing",
    "-fvisibility=hidden",
    "-DIMAGE_COLORSPACE_FLAG",
  ]

  if (is_ohos && is_clang && (target_cpu == "arm" || target_cpu == "arm64")) {
    if (!use_libfuzzer) {
      cflags += [ "-flto=thin" ]
    }
    if (enhanced_opt) {
      if (!use_libfuzzer) {
        cflags += [ "-fwhole-program-vtables" ]
      }
      if (graphic_2d_feature_enable_pgo &&
          graphic_2d_feature_product != "default") {
        if (graphic_2d_feature_enable_codemerge) {
          defines += [ "CM_FEATURE_ENABLE" ]
        }
        cflags += [
          "-fprofile-use=" + rebase_path(
                  "${graphic_2d_feature_pgo_path}/librender_service_base.profdata",
                  root_build_dir),
          "-Wno-error=backend-plugin",
          "-Wno-profile-instr-out-of-date",
          "-Wno-profile-instr-unprofiled",
          "-mllvm",
          "-enable-partial-inlining",
        ]
        inputs = [ "${graphic_2d_feature_pgo_path}/librender_service_base.profdata" ]
      }
      if (graphic_2d_feature_product == "pc" && target_cpu == "arm64") {
        cflags += [ "-moutline-atomics" ]
      }
    }
  }

  if (graphic_2d_feature_product != "wearable") {
    defines += [ "IS_ENABLE_DRM" ]
  }

  cflags_cc = [
    "-std=c++17",
    "-fvisibility-inlines-hidden",
  ]

  public_configs = [ ":export_config" ]

  if (enable_export_macro) {
    defines += [ "ENABLE_EXPORT_MACRO" ]
  }

  if (rs_enable_gpu) {
    defines += [
      "GL_GLEXT_PROTOTYPES",
      "EGL_EGLEXT_PROTOTYPES",
    ]
  }

  if (current_os == "ohos") {
    defines += [ "IS_OHOS" ]
  }

  include_dirs = [
    "$graphic_2d_root/rosen/modules/utils",
    "$graphic_2d_root/interfaces/inner_api/common",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "$graphic_2d_root/rosen/modules/render_service_base/include",
    "$graphic_2d_root/utils/color_manager/export",
    "$graphic_2d_root/utils/log",
    "$graphic_2d_root/rosen/modules/render_service",
  ]

  if (is_arkui_x) {
    include_dirs += [
      "$graphic_2d_root/utils/color_manager/export",
      "//foundation/multimedia/image_framework/interfaces/innerkits/include",
      "//foundation/graphic/graphics_effect/include",
      "//foundation/graphic/graphics_effect/include/core",
      "//foundation/graphic/graphics_effect/include/pipeline",
      "//foundation/graphic/graphics_effect/include/hps",
      "//foundation/graphic/graphics_effect/include/ext",
      "//foundation/graphic/graphics_effect/include/effect",
      "//foundation/graphic/graphics_effect/include/effect/filter",
      "//foundation/graphic/graphics_effect/include/effect/shader",
      "//foundation/graphic/graphics_effect/include/effect/mask",
      "//foundation/graphic/graphics_effect/include/effect/shape",
      "//foundation/graphic/graphics_effect/include/util",
      "//foundation/graphic/graphic_surface/interfaces/inner_api/surface",
    ]
  }

  deps = [
    "$graphic_2d_root/utils/sandbox:sandbox_utils",
    "$rosen_root/modules/platform:hilog",
    "$rosen_root/modules/platform:ipc_core",
  ]

  if (is_arkui_x) {
    deps += [ "//foundation/graphic/graphics_effect:libgraphics_effect" ]
  }

  external_deps = [
    "graphic_surface:surface_headers",
    "graphics_effect:graphics_effect_core",
    "hilog:libhilog",
  ]

  if (graphic_2d_feature_rs_enable_uni_render) {
    external_deps += [ "ability_runtime:ability_manager" ]
  }

  if (use_memmgr_plugin || use_memmgr) {
    external_deps += [ "memmgr:memmgrclient" ]
  }

  if (rosen_is_ohos) {
    external_deps += [
      "hicollie:libhicollie",
      "hisysevent:libhisysevent",
      "image_framework:image_native",
      "lz4:liblz4_shared",
      "init:libbegetutil",
      "ffrt:libffrt",
      "ability_runtime:app_manager",
    ]
  }

  if (enable_compiler_rust) {
    defines += [ "ENABLE_RUST" ]
    sources += get_target_outputs(":particle_cxx_gen")
    include_dirs += [ "${target_gen_dir}/rust/src" ]
    deps += [
      ":particle_cxx_cppdeps",
      ":particle_cxx_gen",
      ":particle_cxx_rust",
    ]
    external_deps += [
      "rust_cxx:cxx_cppdeps",
      "rust_cxx:lib",
    ]
  }

  if (graphic_2d_feature_subtree_parallel_enable) {
    include_dirs += [ "$graphic_2d_ext_root/subtree" ]
  }

  if (defined(graphic_2d_feature_hetero_hdr_enable) && graphic_2d_feature_hetero_hdr_enable
      && defined(graphic_2d_hetero_hdr_configs)) {
    include_dirs += graphic_2d_hetero_hdr_configs.librender_service_hetero_hdr_include
    sources += graphic_2d_hetero_hdr_configs.librender_service_base_hetero_hdr_sources
    external_deps += graphic_2d_hetero_hdr_configs.librender_service_hetero_hdr_external_deps
  }

  if (graphic_2d_feature_mhc_enable) {
    include_dirs += graphic_2d_mhc_configs.librender_service_mhc_include
    sources += graphic_2d_mhc_configs.librender_service_mhc_sources
  }

  if (use_video_processing_engine) {
    defines += [ "USE_VIDEO_PROCESSING_ENGINE" ]
    external_deps += [
      "media_foundation:media_foundation",
      "video_processing_engine:videoprocessingengine",
    ]
    sources += [
      "src/display_engine/rs_vpe_manager.cpp",
      "src/render/rs_colorspace_convert.cpp",
    ]
  }

  if (rosen_is_ohos && defined(enable_ipc_security) && enable_ipc_security) {
    defines += [ "ENABLE_IPC_SECURITY" ]
    external_deps += [
      "access_token:libaccesstoken_sdk",
      "ipc:ipc_core",
    ]
  }

  if (rosen_is_ohos && defined(enable_ipc_security_access_counter) &&
      enable_ipc_security_access_counter) {
    defines += [ "ENABLE_IPC_SECURITY_ACCESS_COUNTER" ]
  }

  if (defined(graphic_2d_math_tools_configs.vendor_root)) {
    sources +=
        graphic_2d_math_tools_configs.librender_service_ext_math_tools_sources
    sources -= graphic_2d_math_tools_configs.librender_service_ext_math_tools_sources_del
  }

  if (defined(use_rosen_drawing) && use_rosen_drawing) {
    defines += [ "USE_ROSEN_DRAWING" ]
    if (rs_enable_gpu) {
      defines += [ "RS_ENABLE_GPU" ]
    }

    deps += [ "$rosen_root/modules/2d_graphics:2d_graphics" ]
  }

  if (defined(enable_full_screen_recongnize) && enable_full_screen_recongnize) {
    defines += [ "ENABLE_FULL_SCREEN_RECONGNIZE" ]
  }

  # for modules/platform
  public_configs += [
    "$rosen_root/modules/platform:eventhandler_config",
    "$rosen_root/modules/platform:image_native_config",
  ]
  public_deps = [
    "$rosen_root/modules/platform:eventhandler",
    "$rosen_root/modules/platform:hilog",
    "$rosen_root/modules/platform:image_native",
    "$rosen_root/modules/platform:ipc_core",
    "src/platform:platform",
  ]

  if (rosen_cross_platform) {
  } else {
    external_deps += [
      "drivers_interface_display:display_commontype_idl_headers",
      "hitrace:hitrace_meter",
    ]
  }

  if (rosen_is_win) {
    defines += [ "_USE_MATH_DEFINES" ]
  }

  if (rosen_is_ohos && graphic_2d_feature_enable_memory_info_manager) {
    defines += [ "RS_MEMORY_INFO_MANAGER" ]
    sources += [ "src/feature/memory_info_manager/rs_memory_info_manager.cpp" ]
  }

  if (defined(ddgr_ext_configs)) {
    libs = ddgr_ext_configs.ddgr_libs
    defines += ddgr_ext_configs.ddgr_defines
    sources += ddgr_ext_configs.libddgr_ext_adaptor_base_sources
    include_dirs += ddgr_ext_configs.libddgr_ext_adaptor_include_dirs
  }

  #profiler
  if (rosen_is_ohos && graphic_2d_feature_rs_enable_profiler &&
      player_framework_enable) {
    defines += [ "RS_PROFILER_ENABLED" ]
    sources += [
      "$graphic_2d_root/rosen/modules/render_service_profiler/rs_profiler_archive.cpp",
      "$graphic_2d_root/rosen/modules/render_service_profiler/rs_profiler_base.cpp",
      "$graphic_2d_root/rosen/modules/render_service_profiler/rs_profiler_cache.cpp",
      "$graphic_2d_root/rosen/modules/render_service_profiler/rs_profiler_dump.cpp",
      "$graphic_2d_root/rosen/modules/render_service_profiler/rs_profiler_pixelmap.cpp",
      "$graphic_2d_root/rosen/modules/render_service_profiler/rs_profiler_settings.cpp",
      "$graphic_2d_root/rosen/modules/render_service_profiler/rs_profiler_utils.cpp",
    ]

    if (defined(global_parts_info) &&
        defined(global_parts_info.open_source_libyuv)) {
      defines += [ "RS_PROFILER_SUPPORTS_PIXELMAP_YUV_EXT" ]
    }
  }
  include_dirs += [ "$graphic_2d_root/rosen/modules/render_service_profiler" ]

  if (defined(global_parts_info) &&
      defined(global_parts_info.resourceschedule_resource_schedule_service)) {
    external_deps += [ "resource_schedule_service:ressched_client" ]
    defines += [ "RES_BASE_SCHED_ENABLE" ]
    external_deps += [ "qos_manager:qos" ]
  }

  part_name = "graphic_2d"
  subsystem_name = "graphic"
}

config("render_service_base_all_deps_config") {
  include_dirs = [
    "include",
    "$rosen_root/modules/2d_graphics/include",
    "$rosen_root/modules/2d_graphics/src",
    "$rosen_root/modules/2d_graphics/src/drawing/engine_adapter",
  ]
}

config("render_service_base_link_config") {
  if (is_ohos && is_clang && (target_cpu == "arm" || target_cpu == "arm64")) {
    ldflags = [
      "-Wl,--lto-O2",
      "-Wl,-mllvm",
      "-Wl,-wholeprogramdevirt-check=fallback",
      "-Wl,-Bsymbolic",
    ]

    if (graphic_2d_feature_enable_pgo && enable_enhanced_opt) {
      ldflags += [
        "-Wl,--aarch64-inline-plt",
        "-Wl,-mllvm,-enable-partial-inlining",
      ]
      if (graphic_2d_feature_enable_codemerge) {
        ldflags += [
          "-Wl,-plugin-opt=-split-machine-functions",
          "-Wl,-mllvm",
          "-Wl,-fixup-unconditional-branch-unsafely",
          "-Wl,--no-create-thunks-introduced-by-mfs",
          "-Wl,-mllvm",
          "-Wl,-mfs-psi-cutoff=999500",
          "-Wl,-z",
          "-Wl,keep-text-section-prefix",
          "-Wl,--symbol-ordering-file=" + rebase_path(
                  "${graphic_2d_feature_pgo_path}/librender_service_base.txt",
                  root_build_dir),
          "-Wl,--no-warn-symbol-ordering",
        ]
      }
    }
    if (target_cpu == "arm64" && defined(build_ext_path)) {
      ldflags += [ 
        "-Wl,--emit-relocs",
        "-Wl,--no-relax", 
        "-mno-fix-cortex-a53-843419" 
      ]
    }
  }
}

ohos_shared_library("librender_service_base") {
  branch_protector_ret = "pac_ret"
  if (!is_mingw && !is_win && !is_linux) {
    sanitize = {
      cfi = false
      cfi_cross_dso = false
      debug = false
      blocklist = "./rs_base_blocklist.txt"
    }
  }

  sources = [ "src/common/rs_common_hook.cpp" ]

  public_configs = [
    ":render_service_base_all_deps_config",
    ":export_config",
    "$graphic_2d_root/utils/socketpair:socketpair_public_config",
    "$graphic_2d_root/rosen/modules/composer/vsync:vsync_public_config",
  ]
  if (enhanced_opt) {
    configs = [ ":render_service_base_link_config" ]
  }

  deps = [ ":render_service_base_src" ]

  # for platform
  public_configs += [
    "$rosen_root/modules/platform:eventhandler_config",
    "$rosen_root/modules/platform:utils_config",
    "$rosen_root/modules/platform:ipc_core_config",
    "$rosen_root/modules/platform:image_native_config",
  ]
  deps += [
    "$rosen_root/modules/platform:platform",
    "$rosen_root/modules/render_service_base/src/platform:platform",
    "$graphic_2d_root/utils/color_manager:color_manager_source",
  ]

  # for header file
  external_deps = []
  public_external_deps = [
    "egl:libEGL",
    "opengles:libGLES",
  ]

  if (rosen_is_ohos) {
    external_deps += [ "hisysevent:libhisysevent" ]
    public_external_deps += [
      "c_utils:utils",
      "graphic_surface:sync_fence",
      "image_framework:image_native",
      "skia:skia_canvaskit",
    ]
  }

  external_deps += [ "hilog:libhilog" ]

  if (rs_enable_gpu) {
    public_external_deps += [ "openssl:libcrypto_shared" ]
  }

  part_name = "graphic_2d"
  subsystem_name = "graphic"
}

ohos_source_set("librender_service_base_static") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = false
    cfi_cross_dso = false
    debug = false
    blocklist = "./rs_base_blocklist.txt"
  }

  public_configs = [ ":render_service_base_all_deps_config" ]

  if (is_arkui_x) {
    sources = [ "src/common/rs_common_hook.cpp" ]
  }

  deps = [ "$rosen_root/modules/platform:eventhandler" ]
  public_deps = [
    ":render_service_base_src",
    "src/platform:platform",
  ]

  external_deps = [ "hilog:libhilog" ]

  public_external_deps = []
  if (rosen_is_ohos) {
    public_external_deps += [
      "graphic_surface:sync_fence",
      "image_framework:image_native",
    ]
  }

  if (rs_enable_gpu) {
    if (is_arkui_x) {
      public_deps += [ "//third_party/openssl:libcrypto_static" ]
    } else {
      public_external_deps += [ "openssl:libcrypto_shared" ]
    }
  }

  part_name = "graphic_2d"
  subsystem_name = "graphic"
}
