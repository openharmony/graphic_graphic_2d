# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/graphic/graphic_2d/graphic_config.gni")

## Build libskeffectchain.so

config("effect_SKeffectChian_config") {
  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
    "-Wall",
    "-Wno-pointer-arith",
    "-Wno-non-virtual-dtor",
    "-Wno-missing-field-initializers",
    "-Wno-c++11-narrowing",
  ]
  if (target_os == "ios") {
    cflags += ["-Wno-ignored-qualifiers"]
  }

  include_dirs = [
    "$graphic_2d_root/rosen/modules/render_service_base/include",
    "$graphic_2d_root/rosen/modules/render_service_base/src",
    "$graphic_2d_root/rosen/modules/effect/color_picker/include",
  ]

  if (!defined(defines)) {
    defines = []
  }
  if (rs_enable_gpu) {
    defines += [ "RS_ENABLE_GPU" ]
  }
}

config("effect_SKeffectChian_public_config") {
  include_dirs = [
    "../egl/include",
    "$graphic_2d_root/utils/log",
    "include",
    "$graphic_2d_root/rosen/modules/2d_graphics",
  ]

  if (is_arkui_x) {
    include_dirs += [
      "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/include",
      "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src",
      "//foundation/multimedia/image_framework/interfaces/innerkits/include",
      "//foundation/graphic/graphic_2d/utils/color_manager/export",
      "//base/hiviewdfx/hilog/interfaces/native/innerkits",
      "//foundation/graphic/graphics_effect/include",
      "//foundation/graphic/graphics_effect/include/core",
      "//foundation/graphic/graphics_effect/include/pipeline",
      "//foundation/graphic/graphics_effect/include/hps",
      "//foundation/graphic/graphics_effect/include/ext",
      "//foundation/graphic/graphics_effect/include/effect",
      "//foundation/graphic/graphics_effect/include/effect/filter",
      "//foundation/graphic/graphics_effect/include/effect/shader",
      "//foundation/graphic/graphics_effect/include/effect/mask",
      "//foundation/graphic/graphics_effect/include/effect/shape",
      "//foundation/graphic/graphics_effect/include/util",
      "//foundation/graphic/graphic_surface/interfaces/inner_api/surface",
      "//foundation/appframework/graphic_2d/${current_os}",
    ]
  }
}
if (is_arkui_x) {
  ohos_source_set("skeffectchain") {
    include_dirs = [
      "//foundation/graphic/graphic_surface/interfaces/inner_api/common",
      "//foundation/graphic/graphic_surface/interfaces/inner_api/utils",
      "//base/hiviewdfx/hitrace/interfaces/native/innerkits/include/hitrace_meter",
    ]

    public_deps = [
      "//base/hiviewdfx/hilog/interfaces/native/innerkits:libhilog_${target_os}",
      "//foundation/arkui/ace_engine/adapter/${target_os}/build:libarkui_${target_os}",
      "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics:2d_graphics",
    ]

    if (target_os == "android") {
      defines = [
        "ANDROID_PLATFORM",
        "ROSEN_ARKUI_X",
      ]
    } else if (target_os == "ios") {
      defines = [ "IOS_PLATFORM" ]
    }

    if (target_os == "android") {
      public_deps +=
          [ "$graphic_2d_root/rosen/modules/effect/egl:libegl_effect" ]

      if (rs_enable_gpu) {
        defines += [ "RS_ENABLE_GPU" ]
        defines += gpu_defines
        public_deps += [
          "$graphic_2d_root/frameworks/opengl_wrapper:EGL",
          "$graphic_2d_root/frameworks/opengl_wrapper:GLESv3",
        ]
      }
    }

    public_external_deps = [
      "c_utils:utilsbase",
      "skia:skia_canvaskit_static",
    ]

    sources = [
      "src/effect_image_chain.cpp",
      "src/effect_image_render.cpp",
    ]

    if (rs_enable_gpu) {
      if (graphic_2d_feature_enable_vulkan) {
        sources += [
          "src/effect_vulkan_context.cpp",
        ]
      }
    }

    configs = [ ":effect_SKeffectChian_config" ]

    public_configs = [ ":effect_SKeffectChian_public_config" ]

    cflags_cc = [ "-std=c++17" ]

    part_name = "graphic_2d"
    subsystem_name = "graphic"
  }
} else {
  ohos_shared_library("skeffectchain") {
    public_deps = [
      "$graphic_2d_root/rosen/modules/effect/egl:libegl_effect",
      "$graphic_2d_root/rosen/modules/2d_graphics:2d_graphics",
    ]

    if (rs_enable_gpu) {
      defines = gpu_defines
      public_deps += [
        "$graphic_2d_root/frameworks/opengl_wrapper:EGL",
        "$graphic_2d_root/frameworks/opengl_wrapper:GLESv3",
      ]
    }

    public_external_deps = [
      "graphic_surface:surface",
      "skia:skia_canvaskit",
    ]

    external_deps = [
      "c_utils:utils",
      "graphics_effect:graphics_effect_core",
      "hilog:libhilog",
      "hitrace:hitrace_meter",
      "image_framework:image",
      "image_framework:image_native",
      "image_framework:pixelconvertadapter",
      "init:libbegetutil",
      "ipc:ipc_core",
      "napi:ace_napi",
      "samgr:samgr_proxy",
    ]

    sources = [
      "src/effect_image_chain.cpp",
      "src/effect_image_render.cpp",
    ]
    if (rs_enable_gpu) {
      if (graphic_2d_feature_enable_vulkan) {
        sources += [
          "src/effect_vulkan_context.cpp",
        ]
      }
    }

    configs = [ ":effect_SKeffectChian_config" ]

    public_configs = [ ":effect_SKeffectChian_public_config" ]

    deps = [
      "$graphic_2d_root/rosen/modules/render_service_base:librender_service_base",
    ]

    cflags_cc = [ "-std=c++17" ]

    install_enable = true
    part_name = "graphic_2d"
    subsystem_name = "graphic"
  }
}
